# Maintainer: Tim Harding <tim@timharding.co>
pkgname='neophyte-git'
pkgver=0.2.4.r3.g748ca36
pkgrel=1
pkgdesc='A WebGPU-rendered Neovim GUI'
arch=('x86_64')
url='https://github.com/tim-harding/neophyte'
license=('MIT')
makedepends=('git' 'rust')
provides=("neophyte=$pkgver")
conflicts=('neophyte')
source=("neophyte::git+$url.git")
sha256sums=('SKIP')

pkgver() {
  cd "neophyte"
  git describe | sed 's/-/.r/;s/-/./'
}

build() {
    cd "neophyte"
    cargo build --release
}

package() {
    cd "neophyte"
    install \
	--mode=0755 \
	-D --target-directory="$pkgdir/usr/bin/" \
	./target/release/neophyte
    install \
	--mode=0644 \
	-D --target-directory="$pkgdir/usr/share/licenses/neophyte/" \
	./LICENSE
    install \
	--mode=0644 \
	-D --target-directory="$pkgdir/usr/share/doc/neophyte/" \
	./README.md
}
